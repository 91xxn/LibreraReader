apply plugin: 'com.android.application'

ext {
    codeNumber = 951
    db = 8
    versionNumber = "8.0.4"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.foobnix.pdf.reader"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode codeNumber
        versionName versionNumber

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            versionNameSuffix ''
            buildConfigField "boolean", "LOG", "false"
        }
        debug{
            buildConfigField "boolean", "LOG", "true"
        }
    }
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    splits {
        // Configures multiple APKs based on ABI.
        abi {
            enable true
            reset()
            include  "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }

    }


    flavorDimensions "version"
    productFlavors {

        beta {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            versionNameSuffix "-beta"
            manifestPlaceholders = [
                    appName:"Beta "+versionNumber,
                    appIcon:"@mipmap/icon_pdf_reader",
                    appRoundIcon:"@mipmap/icon_pdf_reader"
                ]
           
        }

        librera {
            dimension "version"
            applicationId "com.foobnix.pdf.reader"
            versionNameSuffix "-librera"
            manifestPlaceholders = [
                    appName:"Librera Reader",
                    appIcon:"@mipmap/icon_pdf_reader",
                    appRoundIcon:"@mipmap/icon_pdf_reader"
            ]
           
        }

        pro {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            versionNameSuffix "-pro"
            manifestPlaceholders = [
                    appName:"Librera PRO",
                    appIcon:"@mipmap/icon_pdf_pro",
                    appRoundIcon:"@mipmap/icon_pdf_pro"
            ]
        }

        pdf_classic {
            dimension "version"
            applicationId "classic.pdf.reader.viewer.djvu.epub.fb2.txt.mobi.book.reader.lirbi.libri"
            versionNameSuffix "-classic"
            manifestPlaceholders = [
                    appName:"PDF Reader",
                    appIcon:"@mipmap/icon_pdf_classic",
                    appRoundIcon:"@mipmap/launcher_classic"
            ]
           
        }

        ebooka {
            dimension "version"
            applicationId "droid.reader.book.epub.mobi.pdf.djvu.fb2.txt.azw.azw3"
            versionNameSuffix "-ebooka"
            manifestPlaceholders = [
                    appName:"Ebooka Reader",
                    appIcon:"@mipmap/icon_pdf_droid",
                    appRoundIcon:"@mipmap/launcher_droid"
            ]
           
        }

        pdf_v2 {
            dimension "version"
            applicationId "pdf.pdf.reader"
            versionNameSuffix "-pdfv2"
            manifestPlaceholders = [
                    appName:"PDF Reader",
                    appIcon:"@mipmap/icon_pdf_2",
                    appRoundIcon:"@mipmap/launcher_pdf_v2"
            ]
           
        }

        tts_reader {
            dimension "version"
            applicationId "tts.reader"
            versionNameSuffix "-tts"
            manifestPlaceholders = [
                    appName:"TTS Reader",
                    appIcon:"@mipmap/icon_tts_reader_app",
                    appRoundIcon:"@mipmap/launcher_tts_reader_app"
            ]
           
        }

        fdroid {
            dimension "version"
            applicationId "com.foobnix.pro.pdf.reader"
            versionNameSuffix "-fdroid"
            manifestPlaceholders = [
                    appName:"Librera PRO",
                    appIcon:"@mipmap/icon_pdf_pro",
                    appRoundIcon:"@mipmap/icon_pdf_pro"
            ]
        }

    }

    def abiCodes = ['x86':3, 'x86_64':2, 'armeabi-v7a':1, 'arm64-v8a':0]

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavor = variant.productFlavors[0].name.capitalize()
            def abiName = getFilter("ABI")
            def code =abiCodes.get(abiName, 0) + variant.versionCode
            abiName = abiName.replace("arm64-v8a","arm64").replace("armeabi-v7a","arm")

            output.versionCodeOverride = code

            def fullName = "Librera ${flavor}-${versionNumber}-c${code}-d${db}-${abiName}.apk";
            output.outputFileName = fullName;

        }
    }

}

task a_deleteApks(type: Delete) {

    //delete fileTree(dir: 'app/styles' , include: '**/*.css')
}

task a_copyApks(type: Copy) {
    delete fileTree("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing").include("*.apk")

    from file("build/outputs/apk/beta/release")
    include "*.apk"
    into file("/home/ivan-dev/Dropbox/FREE_PDF_APK/testing")
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.android.support:cardview-v7:28+'
    implementation 'com.android.support:support-v4:28+'

    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-api:3.2.2'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.burgstaller:okhttp-digest:1.18'
    implementation 'com.squareup.okio:okio-parent:1.17.3'

    implementation 'com.github.joniles:rtfparserkit:1.13.0'

    implementation 'com.android.support:customtabs:28+'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'


    proImplementation project(':pro')
    fdroidImplementation project(':pro')
    betaImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    libreraImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    pdf_v2Implementation 'com.google.android.gms:play-services-ads:16.0.0'
    ebookaImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    tts_readerImplementation 'com.google.android.gms:play-services-ads:16.0.0'
    pdf_classicImplementation 'com.google.android.gms:play-services-ads:16.0.0'

    proImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    betaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    libreraImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_v2Implementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    ebookaImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    tts_readerImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'
    pdf_classicImplementation 'com.cloudrail:cloudrail-si-android:2.22.4'






}